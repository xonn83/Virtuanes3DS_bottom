; INPUT
;
; v0: vertex 		(x0, y0, z0, w = 1)
; v1: texcoord 		(tx0, ty0)
; v2: color 		
;
; v3: vertex 		(x1, y1, z1, w = 1),		if y < -1, then this is a mode 7 tile.
; v4: texcoord		(tx1, ty1)
; v5: color 		

; OUTPUT
.out 	outpos 	position
.out 	outtex  texcoord0
.out	outcol	color

; Uniforms

.constf MINUSONE 				(-1, -1, -1, -1)

								; if we used 0.00390625, the scanline for mode 7 cannot appear!
.constf ONE_SCANLINE			(-0.00390625, 0.00395, -0.00390625, -0.00390625)   

.gsh
.entry gmain
.proc gmain

	mov		r0, MINUSONE
	mov		r3, v3
	cmp		r3.y, lt, lt, r0.y			; if (v3.y (after projection) < -1) then
	jmpc	cmp.x, mode7

	; Emit the standard:
	;    x0,y0,tx0,ty0     x1,y0,tx1,ty0
	;                 +---+
	;                 |  /|
	;                 | / |
	;                 |/  |
	;                 +---+
	;    x0,y1,tx0,ty1     x1,y1,tx1,ty1
	;
	setemit 0							; top-left
	mov 	outpos.xz, 	v0.xz
	mov		outpos.y,   v0.y
	mov 	outtex.x, 	v1.x
	mov 	outtex.y, 	v1.y
	mov		outcol,		v2
	emit 

	setemit 1							; top-right
	mov 	outpos.xz, 	v3.xz
	mov		outpos.y,   v0.y
	mov 	outtex.x, 	v4.x
	mov 	outtex.y, 	v1.y
	mov		outcol,		v2
	emit 
	
	setemit 2, prim						; bottom-left
	mov 	outpos.xz, 	v0.xz
	mov		outpos.y,   v3.y
	mov 	outtex.x, 	v1.x
	mov 	outtex.y, 	v4.y
	mov		outcol,		v2
	emit 

	setemit 0, prim	inv					; bottom-right
	mov 	outpos.xz, 	v3.xz
	mov		outpos.y,   v3.y
	mov 	outtex.x, 	v4.x
	mov 	outtex.y, 	v4.y
	mov		outcol,		v2
	emit 

	end
	nop

mode7:

	; Emit the special case for mode 7.
	;
	;    x0,y0,z0,tx0,ty0     x1,y0,z0,tx1,ty1
	;                    +---+
	;                    |  /|
	;                    | / |
	;                    |/  |
	;                    +---+
	;    x0,y1,z0,tx0,ty0     x1,y1,z0,tx1,ty1
	;
	mov		r1.y, ONE_SCANLINE
	mov		r0.y, v0.y
	add 	r3.y, v0.y, r1.y			; r3.y = v0.y + 1 scan line

	setemit 0							; top-left
	mov 	outpos.xz, 	v0.xz
	mov		outpos.y,   r0.y
	mov 	outtex.x, 	v1.x
	mov 	outtex.y, 	v1.y
	mov		outcol,		v2
	emit 

	setemit 1							; top-right
	mov 	outpos.xz, 	v3.xz
	mov		outpos.y,   r0.y
	mov		outpos.z,	v0.z
	mov 	outtex.x, 	v4.x
	mov 	outtex.y, 	v4.y
	mov		outcol,		v2
	emit 
	
	setemit 2, prim						; bottom-left
	mov 	outpos.xz, 	v0.xz
	mov		outpos.y,   r3.y
	mov 	outtex.x, 	v1.x
	mov 	outtex.y, 	v1.y
	mov		outcol,		v2
	emit 

	setemit 0, prim	inv					; bottom-right
	mov 	outpos.xz, 	v3.xz
	mov		outpos.y,   r3.y
	mov		outpos.z,	v0.z
	mov 	outtex.x, 	v4.x
	mov 	outtex.y, 	v4.y
	mov		outcol,		v2
	emit 

	end
	nop

.end